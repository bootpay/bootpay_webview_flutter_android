group = 'kr.co.bootpay.webviewflutter'
version = '1.0-SNAPSHOT'

buildscript {
    // Use host app's kotlin_version if available, otherwise use 1.7.10 for maximum compatibility
    // Supports Kotlin 1.7.0+ (tested with 1.7.x, 1.8.x, 1.9.x, 2.0.x)
    ext.kotlin_version = rootProject.ext.has('kotlin_version')
        ? rootProject.ext.get('kotlin_version')
        : '1.7.10'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace = "kr.co.bootpay.webviewflutter"
    // Use flutter.compileSdkVersion with fallback to 34 for older projects
    compileSdk = project.hasProperty('flutter') && flutter.hasProperty('compileSdkVersion')
        ? flutter.compileSdkVersion
        : 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        minSdkVersion 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lint {
        checkAllWarnings = true
        warningsAsErrors = true
        disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency', 'NewerVersionAvailable'
        baseline = file("lint-baseline.xml")
    }

    dependencies {
        // Use Kotlin 1.7.x compatible versions for maximum compatibility
        implementation("androidx.annotation:annotation:1.5.0")
        implementation("androidx.webkit:webkit:1.8.0")
        testImplementation("junit:junit:4.13.2")
        testImplementation("org.mockito:mockito-core:5.3.1")
        testImplementation("org.mockito:mockito-inline:5.2.0")
        testImplementation("androidx.test:core:1.5.0")
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
